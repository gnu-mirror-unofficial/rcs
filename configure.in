# Configure template for RCS

# Copyright (C) 2010 Thien-Thi Nguyen
# Copyright (C) 1995 Paul Eggert
#
# This file is part of GNU RCS.
#
# GNU RCS is free software: you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# GNU RCS is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty
# of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

AC_INIT([GNU RCS],[5.8-dev],[bug-rcs@gnu.org])
AC_CONFIG_HEADERS([src/auto-sussed.h])
AC_CONFIG_AUX_DIR([build-aux])

AC_ARG_ENABLE([compat2],
 [AS_HELP_STRING([--enable-compat2],[enable reading of RCS 2.x
   files (really ancient, prior to GNU RCS).  If you enable this,
   consider hiring ttn to migrate your files to a format more
   suited to this millenium!  (The file format output by RCS 2.x
   became obsolete in 1982.)])])

if test yes = "$enable_compat2"
  then enable_compat2=1
  else enable_compat2=0
fi
AC_DEFINE_UNQUOTED([COMPAT2],[$enable_compat2],
  [Support reading RCS 2.x files?])

AC_ARG_WITH([mailer],
 [AS_HELP_STRING([--with-mailer=PROG],[use PROG to send mail
   when a lock is broken (default: don't send mail)])],[
  if test no = "$withval" ; then
    AS_UNSET([with_mailer])
  elif test 0 = `expr "$withval" : '/*'` ; then
    AC_MSG_ERROR([--with-mailer PROG not absolute: $withval])
  fi
])
if test "$with_mailer" ; then
AC_DEFINE_UNQUOTED([SENDMAIL],["$with_mailer"],
  [Program used to send mail when a lock is broken.])
fi

AC_ARG_WITH([diffutils],
 [AS_HELP_STRING([--with-diffutils],
                 [assume GNU diffutils is similarly installed])],[
  if test yes = "$withval" ; then
    : ${DIFF='$(bindir)/diff'}
    : ${DIFF3=${DIFF}3}
    : ${DIFF3_BIN=1}
    : ${DIFFFLAGS=-an}
    : ${DIFF_FAILURE=1}
    : ${DIFF_L=1}
    : ${DIFF_SUCCESS=0}
    : ${DIFF_TROUBLE=2}
  fi
])

# Set up simple `diff' test.
echo 0 >conftest0
echo 0 >conftest0c
echo 1 >conftest1
{ echo d1 1 ; echo a1 1 ; echo 1 ; } >conftestok

# Set DIFF to the name of the `diff' program to be run.
# On some systems, the RCS-compatible diff program is called `rdiff';
# use it if it works and `diff' doesn't.
AC_SUBST(DIFF)
AC_MSG_CHECKING([diff basename])
AC_CACHE_VAL(rcs_cv_prog_diff, [
  rcs_cv_prog_diff=$DIFF
  case $rcs_cv_prog_diff in
  '')
    for i in diff /usr/lib/rdiff rdiff
    do
      sh -c "exec $i -n conftest0 conftest1" >conftestout 2>/dev/null
      case $? in
      1)
	if cmp -s conftestok conftestout
	then rcs_cv_prog_diff=$i; break
	fi
	;;
      esac
    done
    ;;
  esac
])
DIFF=$rcs_cv_prog_diff
case $DIFF in
'') AC_MSG_ERROR(cannot find RCS-compatible diff);;
esac
AC_MSG_RESULT($DIFF)
AC_PATH_PROG(DIFF, $DIFF, $DIFF)

# Set DIFF_SUCCESS, DIFF_FAILURE, DIFF_TROUBLE to diff's exit status
# when it finds no differences, some differences, or trouble.
AC_MSG_CHECKING([diff success status])
AC_CACHE_VAL(rcs_cv_status_diff_success, [
  rcs_cv_status_diff_success=$DIFF_SUCCESS
  case $rcs_cv_status_diff_success in
  '')
    # We can't use `$DIFF conftest0 conftest0',
    # since buggy NEXTSTEP 3.0 diff silently yields exit status 2 for this.
    $DIFF conftest0 conftest0c >/dev/null 2>&1
    rcs_cv_status_diff_success=$?
    ;;
  esac
])
DIFF_SUCCESS=$rcs_cv_status_diff_success
AC_MSG_RESULT($DIFF_SUCCESS)
AC_DEFINE_UNQUOTED([DIFF_SUCCESS],[$DIFF_SUCCESS],
  [Exit status when diff(1) finds no differences.])
#
AC_MSG_CHECKING([diff failure status])
AC_CACHE_VAL(rcs_cv_status_diff_failure, [
  rcs_cv_status_diff_failure=$DIFF_FAILURE
  case $rcs_cv_status_diff_failure in
  '')
    $DIFF conftest0 conftest1 >/dev/null 2>&1
    rcs_cv_status_diff_failure=$?
    ;;
  esac
])
DIFF_FAILURE=$rcs_cv_status_diff_failure
AC_MSG_RESULT($DIFF_FAILURE)
AC_DEFINE_UNQUOTED([DIFF_FAILURE],[$DIFF_FAILURE],
  [Exit status when diff(1) finds some differences.])
#
AC_MSG_CHECKING([diff trouble status])
AC_CACHE_VAL(rcs_cv_status_diff_trouble, [
  rcs_cv_status_diff_trouble=$DIFF_TROUBLE
  case $rcs_cv_status_diff_trouble in
  '')
    $DIFF conftest0 no/such/file >/dev/null 2>&1
    rcs_cv_status_diff_trouble=$?
    ;;
  esac
])
DIFF_TROUBLE=$rcs_cv_status_diff_trouble
AC_MSG_RESULT($DIFF_TROUBLE)
AC_DEFINE_UNQUOTED([DIFF_TROUBLE],[$DIFF_TROUBLE],
  [Exit status when diff(1) finds trouble.])

# Set DIFFFLAGS to the options of the `diff' program to be run.
# Use -an if possible, -n otherwise.
AC_MSG_CHECKING([diff options for RCS])
AC_CACHE_VAL(rcs_cv_options_diff, [
  rcs_cv_options_diff=$DIFFFLAGS
  case $rcs_cv_options_diff in
  '')
    rcs_cv_options_diff=-n
    $DIFF -an conftest0 conftest1 >conftestout 2>conftestout2
    case $? in
    1)
      if cmp -s conftestok conftestout && test ! -s conftestout2
      then rcs_cv_options_diff=-an
      fi
      ;;
    esac
    ;;
  esac
])
DIFFFLAGS=$rcs_cv_options_diff
AC_MSG_RESULT($DIFFFLAGS)
AC_DEFINE_UNQUOTED([DIFFFLAGS],["$DIFFFLAGS"],
  [Options of the `diff' program to be run.])

# Set DIFF_L to 1 if diff understands the L option, 0 otherwise.
AC_MSG_CHECKING([diff -L])
AC_CACHE_VAL(rcs_cv_options_diff_l, [
  rcs_cv_options_diff_l=$DIFF_L
  case $rcs_cv_options_diff_l in
  '')
    rcs_cv_options_diff_l=0
    $DIFF -c -L 0 -L 1 conftest0 conftest1 >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
*** 0
--- 1
***************
*** 1 ****
! 0
--- 1 ----
! 1
EOF
      then rcs_cv_options_diff_l=1
      fi
      ;;
    esac
    ;;
  esac
])
DIFF_L=$rcs_cv_options_diff_l
case $DIFF_L in
1) AC_MSG_RESULT(yes);;
*) AC_MSG_RESULT(no);;
esac
AC_DEFINE_UNQUOTED([DIFF_L],[$DIFF_L],
  [Does diff(3) understand -L?])

# Set DIFF3 to the name of the diff3 program.
# In some systems (e.g. BSD/OS 2.0), diffutils diff3 lives in /usr/libexec.
diff3PATH=$PATH:/usr/libexec
AC_SUBST(DIFF3)
AC_MSG_CHECKING([diff3 -m])
AC_CACHE_VAL(rcs_cv_prog_diff3_bin, [
  rcs_cv_prog_diff3_bin=$DIFF3
  case $rcs_cv_prog_diff3_bin in
  '')
    PATH=$diff3PATH sh -c "exec diff3 -E -m -L 0 -L 1 -L 2 conftest0 conftest1 /dev/null" >conftestout 2>/dev/null
    case $? in
    1)
      if cmp -s - conftestout <<'EOF'
<<<<<<< 0
0
=======
>>>>>>> 2
EOF
      then rcs_cv_prog_diff3_bin=diff3
      fi
      ;;
    esac
  ;;
  esac
])
case $rcs_cv_prog_diff3_bin in
?*)
  AC_MSG_RESULT(yes)
  ac_save_path=$PATH
  PATH=$diff3PATH
  AC_PATH_PROG(DIFF3, $rcs_cv_prog_diff3_bin, $rcs_cv_prog_diff3_bin)
  PATH=$ac_save_path
  ;;
'')
  AC_MSG_RESULT(no)
  AC_MSG_CHECKING([diff3 library program])
  dnl We can't use AC_PATH_PROG since we don't want to inspect /bin,
  dnl and AC_PATH_PROG uses `test'.
  AC_CACHE_VAL(rcs_cv_path_diff3_lib, [
    $DIFF conftest0 conftest1 >conftest01
    $DIFF /dev/null conftest1 >conftestn1
    for i in /usr/*lib*/*diff3*; do
      sh -c "exec $i -E conftest01 conftestn1 conftest0 /dev/null conftest1" >conftestout 2>/dev/null
      # The exit status is arbitrary!  Test the output a bit.
      if
	grep '^<<* *conftest0$' conftestout >/dev/null 2>&1 &&
	grep '^>>* *conftest1$' conftestout >/dev/null 2>&1 &&
	grep '^0a$' conftestout >/dev/null 2>&1
      then
	rcs_cv_path_diff3_lib=$i
	break
      fi
    done
  ])
  DIFF3=$rcs_cv_path_diff3_lib
  case $DIFF3 in
  '') AC_MSG_ERROR(cannot find a working diff3 library program);;
  ?*) AC_MSG_RESULT($DIFF3);;
  esac
  ;;
esac

case $DIFF3_BIN in
'')
  case $rcs_cv_prog_diff3_bin in
  '') DIFF3_BIN=0;;
  ?*) DIFF3_BIN=1;;
  esac
  ;;
esac
AC_DEFINE_UNQUOTED([DIFF3_BIN],[$DIFF3_BIN],
  [Is the diff3 program the user-visible GNU diff3?])

# Clean up simple `diff' test.
rm -f conftest*

AC_DEFUN([eddesc],
  [[The singular ed(1), necessary only if we don't have diff3(1).]])
AC_ARG_VAR([ED],eddesc)
if test 0 = $DIFF3_BIN ; then
  AC_PATH_PROG([ED], [ed], [ed])
  AC_DEFINE_UNQUOTED([ED],["$ED"],eddesc)
fi

# Use the GNU pic -n option if available; it avoids GNU extensions,
# which is need for proper operation to generate a portable man page.
# Similarly, if using traditional pic, use its -D option.
AC_CHECK_PROGS(PIC, "pic -n" "gpic -n" "pic -D" "pic", pic)

AC_PROG_CC_C99

if test x"$ac_cv_prog_cc_c99" = xno ; then
   AC_MSG_ERROR([Could not find a C99-capable compiler])
fi

AC_DEFUN([RCS_CHECK_GCC_ATTRIBUTE],[
AS_VAR_PUSHDEF([GAV],[rcs_cv_gcc_$1])dnl
  if test yes = "$GCC" ; then
    AC_CACHE_CHECK([if gcc supports "$1" attribute],[GAV],
      [AC_TRY_COMPILE([$2],,GAV=yes, GAV=no)])
  fi
  if test yes = "$GAV" ; then
    AC_DEFINE([GCC_HAS_ATTRIBUTE_]m4_toupper($1),[1],
     [Define if gcc supports attribute "$1".])
  fi
AS_VAR_POPDEF([GAV])dnl
])

RCS_CHECK_GCC_ATTRIBUTE([noreturn],[
  extern void func (void) __attribute__ ((noreturn));
])
RCS_CHECK_GCC_ATTRIBUTE([format],[
  extern void func (const char *, ...)
     __attribute__ ((format (printf, 1, 2)));
])

AC_PROG_INSTALL
AC_PROG_MAKE_SET

# headers
AC_CHECK_HEADERS_ONCE([
  dirent.h fcntl.h limits.h mach/mach.h net/errno.h
  pwd.h siginfo.h signal.h
  sys/mman.h sys/wait.h ucontext.h unistd.h utime.h
])

# types -- see also: (info "(autoconf) Particular Types")
AC_TYPE_MODE_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SSIZE_T
AC_TYPE_UID_T
AC_CHECK_TYPE([sig_atomic_t],,[dnl
  AC_DEFINE([sig_atomic_t], int,
    [Define to `int' if <signal.h> does not define.])
],[dnl
#ifdef HAVE_SIGNAL_H
# include <signal.h>
#endif
])

# funcs

AC_FUNC_FORK
AC_CHECK_FUNCS_ONCE([
  fchmod
  ftruncate
  getcwd
  getpwuid
  getuid
  getwd
  madvise
  mkstemp
  mmap
  psiginfo
  psignal
  readlink
  seteuid
  setuid
  sigaction
  sigblock
  sys_siglist
  waitpid
])

if test yes = $ac_cv_func_psiginfo ; then
AC_CHECK_MEMBER([struct siginfo_t.si_errno],[
  AC_DEFINE([HAVE_SI_ERRNO],1,
    [Define if `struct siginfo_t' has member `si_errno'.])
],,[[
#include <signal.h>
]])
fi

# specific behaviors (that unfortunately require running a program to check)

AC_DEFUN([RCS_CBOOL],[dnl
dnl $1 -- C variable (actually #define) name
dnl $2 -- sense that maps to "1" {yes, no}
dnl $3 -- shell variable name to check
dnl $4 -- description for the config header file
if test $2 = $[]$3 ; then cbool=1 ; else cbool=0 ; fi
AC_DEFINE_UNQUOTED([$1],[$cbool],[$4])dnl
])dnl

AC_CACHE_CHECK([if `fopen (F, "w+")' truncates],[rcs_cv_fopen_truncates],[
  echo nonempty > conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <stdio.h>
]],[[
  return ! fopen ("conftest.data", "w+");
]])
],[res=yes],[res=no])
  test yes = $res && test ! -s conftest.data || res=no
  rcs_cv_fopen_truncates=$res
])
RCS_CBOOL([BAD_FOPEN_WPLUS],[no],[rcs_cv_fopen_truncates],
  [Does `fopen (F, "w+")' fail to truncate?])

if test yes = $ac_cv_header_signal_h &&
   test  no = $ac_cv_func_sigaction ; then
AC_CACHE_CHECK([if signal zaps the handler],[rcs_cv_sig_zaps],[
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <signal.h>
static void nothing (i) int i; {}
]],[[
  int count = 2;
  signal (SIGINT, nothing);
  while (--count)
    raise (SIGINT);
]])
],[res=no],[res=yes])
  rcs_cv_sig_zaps=$res
])
else rcs_cv_sig_zaps=no
fi
RCS_CBOOL([SIG_ZAPS_HANDLER],[yes],[rcs_cv_sig_zaps],
  [Does a signal zap its handler (requiring another call to `signal')?])

AC_CACHE_CHECK([if `write' after `creat (F, 0)' works],[rcs_cv_wac_ok],[
  rm -f conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <unistd.h>
#include <sys/types.h>
#include <sys/stat.h>
#include <fcntl.h>
#if defined(O_CREAT) && defined(O_WRONLY)
#	define creat0(f) open(f, O_CREAT|O_WRONLY, 0)
#else
#	define creat0(f) creat(f, 0)
#endif
char buf[17000];
]],[[
  int f;
  return (0 > (f = creat0 ("conftest.data"))
          || sizeof (buf) != write (f, buf, sizeof (buf))
          || 0 > close (f));
]])
],[res=yes],[res=no])
  if test yes = "$res" && test -f conftest.data && test ! -w conftest.data
    then :
    else res=no
  fi
  rcs_cv_wac_ok=$res
])
RCS_CBOOL([BAD_CREAT0],[no],[rcs_cv_wac_ok],
  [Does `write' after `creat (F, 0)' fail in the wrong way?])

AC_CACHE_CHECK([if `fflush' works on input streams],[rcs_cv_flush_in_ok],[
  rm -f conftest.data
  echo nonempty > conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <sys/types.h>
#include <unistd.h>
#include <stdio.h>
]],[
  FILE *fp = fopen ("conftest.data", "r");
  return (EOF == getc (fp)
          || 0 > fseek (fp, 0L, SEEK_SET)
          || 0 > fflush (fp)
          || 0 > lseek (fileno (fp), 0, SEEK_CUR));
])
],[res=yes],[res=no])
  rcs_cv_flush_in_ok=$res
])
RCS_CBOOL([CAN_FFLUSH_IN],[yes],[rcs_cv_flush_in_ok],
  [Does `fflush' work on input streams?])

AC_CACHE_CHECK([if `unlink' works on unwritable files],[rcs_cv_unlink_mw_ok],[
  touch conftest.data
  chmod -w conftest.data
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <unistd.h>
]],[
  return 0 > unlink ("conftest.data");
])
],[res=yes],[res=no])
  test yes = $res && test -f conftest.data && res=no
  rcs_cv_unlink_mw_ok=$res
])
RCS_CBOOL([BAD_UNLINK],[no],[rcs_cv_unlink_mw_ok],
  [Does `unlink' fail on unwritable files?])

AC_CACHE_CHECK([if `rename' handles unwritable source/target],
               [rcs_cv_rename_mw],[
m4_pushdef([ZONK],[rm -f conftest-A conftest-B && echo A > conftest-A])dnl
m4_pushdef([GOOD],[test ! -f conftest-A && test -f conftest-B])dnl
m4_pushdef([HMMM],[./conftest$EXEEXT && GOOD])dnl
  ZONK
  res=hopeless
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <unistd.h>
]],[
  return 0 > rename ("conftest-A", "conftest-B");
])
],[
  if GOOD ; then
    res=ok
    # unwritable source
    ZONK ; chmod -w conftest-A
    HMMM && AS_VAR_APPEND([res],[,source=ok])
    # unwritable target
    ZONK ; echo B > conftest-B ; chmod -w conftest-B
    HMMM && AS_VAR_APPEND([res],[,target=ok])
  fi
])
  rcs_cv_rename_mw=$res
m4_popdef([HMMM])dnl
m4_popdef([GOOD])dnl
m4_popdef([ZONK])dnl
])
rename_source=no
rename_target=no
AS_IF([test hopeless = $rcs_cv_rename_mw],,
  [eval `echo $rcs_cv_rename_mw | sed 's/^..//;s/,/ rename_/g'`])
RCS_CBOOL([BAD_A_RENAME],[no],[rename_source],
  [Does `rename' fail if the source is unwritable?])
RCS_CBOOL([BAD_B_RENAME],[no],[rename_target],
  [Does `rename' fail if the target is unwritable?])

AC_DEFUN([SIMPLE_MMAP_PROGRAM],[AC_LANG_PROGRAM([[
#include <stdio.h>
#include <fcntl.h>
#include <sys/types.h>
#include <sys/stat.h>
#include <sys/mman.h>
]],[
  struct stat b;
  char *a;
  int fd, fc;

  fd = open ("conftest.data", O_RDONLY);
  fstat (fd, &b);
  a = mmap (NULL, b.st_size, PROT_READ, MAP_SHARED, fd, 0);
  if ((char *) MAP_FAILED == a)
    {
      perror ("mmap");
      return 1;
    }
  /* Finish.  */
$1
])])dnl

if test yes = $ac_cv_func_mmap &&
   test yes = $ac_cv_header_sys_mman_h ; then
AC_CACHE_CHECK([that `mmap' is sane],[rcs_cv_sane_mmap],[
  rcs_cv_sane_mmap=yes
  # AIX 3.2.0 read-only mmap updates last-modified time of file!
  if uname -a | grep AIX >/dev/null ; then
    echo nonempty > conftest.data
    bef=`date -r conftest.data`
    sleep 2
    AC_RUN_IFELSE([SIMPLE_MMAP_PROGRAM])
    aft=`date -r conftest.data`
    test "$bef" = "$aft" || rcs_cv_sane_mmap=no
  fi
])
  if test yes = "$rcs_cv_sane_mmap" ; then
AC_CACHE_CHECK([signal received if referencing nonexistent part of mmapped file],
               [rcs_cv_mmap_signal],[
    res=SIGBUS
    echo nonempty > conftest.data
    AC_RUN_IFELSE([SIMPLE_MMAP_PROGRAM([dnl
      fc = creat ("conftest.data", 0);
      /* Refer to nonexistent storage, causing a signal.  */
      printf ("%d %c\n", fc, *a);
])],[res=unknown],[res=$?])
    # If there is no signal, better to disable mmap entirely.
    AS_IF([test unknown = $res],[res=0],
          [test SIGBUS = $res],[:],
          [test 128 -ge $res],[res=SIGBUS],
          [AS_VAR_ARITH([res],[$res - 128])])
    rcs_cv_mmap_signal=$res
])
AC_DEFINE_UNQUOTED([MMAP_SIGNAL],[$rcs_cv_mmap_signal],
  [Signal received if referencing nonexistent part of mmapped file.])
  fi
fi

AC_CACHE_CHECK([if `wait' can handle ignored SIGCHLD],[rcs_cv_tolerant_wait],[
AC_RUN_IFELSE([AC_LANG_PROGRAM([[
#include <sys/types.h>
#include <errno.h>
#include <signal.h>
#include <sys/wait.h>
#include <unistd.h>
]],[
  signal (SIGCHLD, SIG_IGN);
#ifdef HAVE_WORKING_FORK
  {
    int status;
    pid_t p = fork ();
    if (p < 0)
      {
	perror ("fork");
	return 2;
      }
    if (!p)
      _exit (0);
    while (wait (&status) != p)
      {
	if (ECHILD == errno)
          return 1;
        if (EINTR != errno)
          {
	    perror ("wait");
	    return 2;
	  }
      }
  }
#else
  if (0 > system ("true"))
    return 1;
#endif
])dnl
],[res=yes],[res=no])
  rcs_cv_tolerant_wait=$res
])
RCS_CBOOL([BAD_WAIT_IF_SIGCHLD_IGNORED],[no],[rcs_cv_tolerant_wait],
  [Does ignoring SIGCHLD break `wait'?])

# miscellaneous

AS_IF([test 1 = `expr "$ac_pwd" : '/*'`],[woe=no],[woe=yes])
RCS_CBOOL([WOE],[yes],[woe],[Are we trying to "support" MS-DOS?])

AC_DEFUN([WOEMUX],[AS_IF([test yes = $woe],[$2],[$1])])

WOEMUX([ABSFILENAMEGLOB='/*'],[ABSFILENAMEGLOB='?:*'])
AC_SUBST([ABSFILENAMEGLOB])

AC_CACHE_CHECK([if // and / are the same],[rcs_cv_slsl_is_sl],[
m4_pushdef([inode],[ls -d -i $1 | sed 's/^ *//;s/ .*//'])dnl
  WOEMUX([fsrootdir=/],[fsrootdir=\\])
  if test `inode([$fsrootdir])` = `inode([${fsrootdir}${fsrootdir}])`
  then rcs_cv_slsl_is_sl=yes
  else rcs_cv_slsl_is_sl=no
  fi
popdef([inode])dnl
])
RCS_CBOOL([SLASHSLASH_IS_SLASH],[yes],[rcs_cv_slsl_is_sl],
  [Are // and / the same directory?])

# write it out

AC_CONFIG_FILES([
  Makefile
  man/Makefile
  src/Makefile
])
AC_CONFIG_FILES([build-aux/abs-p],[chmod +x build-aux/abs-p])

AC_OUTPUT
